<?altova_samplexml i:\eqf\p0233001.f00\rpt.xml?>
<!--
    Copyright (c) 1999-2015, International Business Machines
    Corporation and others.  All rights reserved.
-->

<!-- &nbsp; does not exists in XPath, so defined as the real unicode character 160
     which is the non breaking space character and provides exactly the same result than using &nbsp; -->
     
<!DOCTYPE xsl:stylesheet 
	[
		<!ENTITY nbsp "&#160;">
	]>

<!-- calrelp.xsl

	Author: Gerhard Queck

	Purpose: Convert an XML calculation report to HTML
-->
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

	<xsl:output method="html" encoding="utf-8"/>

	<xsl:template match="/">
		<html>
<!-- <head> required to put <META http-equiv="Content-Type" content="text/html; charset=utf-8">
     generated by output encoding attribute -->
     		<head></head>
			<body>
				<xsl:call-template name="reportheader" />
			    <xsl:for-each select="/tmwordcount/description/report/option">
					<xsl:choose>
						<xsl:when test="./@name = 'Base' ">
							<xsl:call-template name="base" />
						</xsl:when>
						<xsl:when test="./@name = 'Summary' ">
							<xsl:call-template name="summary" />
						</xsl:when>
						<xsl:when test="./@name = 'FactSheet' ">
							<xsl:call-template name="factsheet" />
							<xsl:call-template name="finalsummary" />
						</xsl:when>
					</xsl:choose>
			    </xsl:for-each>
			    <xsl:if test="/tmwordcount/description/report/@withDocumentList='Yes'">
			      <xsl:call-template name="documentlist"/>
			    </xsl:if>
			    
			</body>
		</html>
	</xsl:template>
	
<!-- create report header --> 
<xsl:template name = "reportheader" >
	<P><BR/></P>
	<CENTER>
	<TABLE border="1">
	   <TBODY>
		  <TR>
			 <TD bgcolor="#d3d3d3">Report:</TD>
			 <TD bgcolor="#f7f7f7">
				 <xsl:value-of select="/tmwordcount/description/report/@type"/>&nbsp;
			    <xsl:for-each select="/tmwordcount/description/report/option">
					<xsl:value-of select="./@name"/>,&nbsp;
			    </xsl:for-each>
			  </TD>
		  </TR>
		  <TR>
			 <TD bgcolor="#d3d3d3">OpenTM2 Version:</TD>
			 <TD bgcolor="#f7f7f7">
			    <xsl:value-of select="/tmwordcount/description/translationManager/@version"/>.<xsl:value-of select="/tmwordcount/description/translationManager/@release"/>
			    <xsl:text>&nbsp;Build </xsl:text><xsl:value-of select="/tmwordcount/description/translationManager/@build"/>
			 </TD>
		  </TR>
		  <TR>
			 <TD bgcolor="#d3d3d3">Calculation Profile:</TD>
			 <TD bgcolor="#f7f7f7"><xsl:value-of select="/tmwordcount/description/profile/@name"/></TD>
		  </TR>
		  <TR>
			 <TD bgcolor="#d3d3d3">Report Generated at:</TD>
			 <TD bgcolor="#f7f7f7">
				 <xsl:value-of select="/tmwordcount/description/report/@generatedDate"/>
				 &nbsp;
				 <xsl:value-of select="/tmwordcount/description/report/@generatedTime"/>
			</TD>
		  </TR>
		  <TR>
			 <TD bgcolor="#d3d3d3">Folder Name:</TD>
			 <TD bgcolor="#f7f7f7"><xsl:value-of select="/tmwordcount/description/folder/@nameLong"/></TD>
		  </TR>
		  <TR>
			 <TD bgcolor="#d3d3d3">Documents in Folder:</TD>
			 <TD bgcolor="#f7f7f7"><xsl:value-of select="/tmwordcount/description/folder/@documentsAmount"/></TD>
		  </TR>
		  <TR>
			 <TD bgcolor="#d3d3d3">Documents selected:</TD>
			 <TD bgcolor="#f7f7f7"><xsl:value-of select="/tmwordcount/description/report/@documentsSelected"/></TD>
		  </TR>
		  <xsl:if test="/tmwordcount/description/report/@documentsWithRedundantInfo != ''">
		  <TR>
			 <TD bgcolor="#d3d3d3">Documents prepared for redundany report:</TD>
			 <TD bgcolor="#f7f7f7"><xsl:value-of select="/tmwordcount/description/report/@documentsWithRedundantInfo"/></TD>
		  </TR>
		  </xsl:if>
		  <TR>
			 <TD bgcolor="#d3d3d3">Target Language:</TD>
			 <TD bgcolor="#f7f7f7"><xsl:value-of select="/tmwordcount/description/folder/@targetLanguage"/></TD>
		  </TR>
	   </TBODY>
	</TABLE>
	</CENTER>
</xsl:template>

<!-- create base report --> 
<xsl:template name = "base" >
	<P><BR/></P>
	<TABLE border="1">
	   <TBODY>
		  <TR>
			 <TD bgcolor="#d3d3d3">Word Count: </TD>
			 <TD bgcolor="#f7f7f7">Base</TD>
		  </TR>
		  <TR>
			 <TD bgcolor="#d3d3d3">Folder:</TD>
			 <TD bgcolor="#f7f7f7"><xsl:value-of select="/tmwordcount/description/folder/@nameLong"/></TD>
		  </TR>
		  <TR>
			 <TD bgcolor="#d3d3d3">Counted:</TD>
			 <TD bgcolor="#f7f7f7">Source Words</TD>
		  </TR>
	   </TBODY>
	</TABLE>
	<xsl:for-each select="/tmwordcount/documents/document">
		<xsl:call-template name="base-one-document">
			<xsl:with-param name="document" select="." />
		</xsl:call-template>
</xsl:for-each>

</xsl:template>

<!-- create base report for one document --> 
<xsl:template name = "base-one-document" >
	<xsl:param name="document"></xsl:param>
	<P>
	<TABLE border="1">
	   <TBODY>
		  <TR>
			 <TD bgcolor="#d3d3d3">Document: </TD>
			 <TD bgcolor="#f7f7f7"><xsl:value-of select="$document/@nameLong"/></TD>
		  </TR>
		  <xsl:if test="$document/@path != '' ">
			  <TR>
				 <TD bgcolor="#d3d3d3">Path: </TD>
				 <TD bgcolor="#f7f7f7"><xsl:value-of select="$document/@path"/></TD>
			  </TR>
		  </xsl:if>
		  <TR>
			 <TD bgcolor="#d3d3d3">Folder:</TD>
			 <TD bgcolor="#f7f7f7"><xsl:value-of select="/tmwordcount/description/folder/@nameLong"/></TD>
		  </TR>
		  <xsl:if test="$document/@memory != '' ">
			  <TR>
				 <TD bgcolor="#d3d3d3">Memory: </TD>
				 <TD bgcolor="#f7f7f7"><xsl:value-of select="$document/@memory"/></TD>
			  </TR>
		  </xsl:if>
		  <TR>
			 <TD bgcolor="#d3d3d3">Counted:</TD>
			 <TD bgcolor="#f7f7f7">Source Words</TD>
		  </TR>
	   </TBODY>
	</TABLE>
	<TABLE border="1" cellspacing="3">
	   <TBODY>
     	    <xsl:call-template name="countsheader">
				<xsl:with-param name="factsheet">No</xsl:with-param>
			</xsl:call-template>
			<xsl:for-each select="$document/wordcount">
				<xsl:if test="./@shipmentFolder = 'All Shipments' ">
					<xsl:call-template name="wordcounts">
						<xsl:with-param name="counts" select="." />
						<xsl:with-param name="name" >Sum</xsl:with-param>
						<xsl:with-param name="id" >0</xsl:with-param>
					</xsl:call-template>
				</xsl:if>
				<xsl:if test="./@shipmentFolder != 'All Shipments' ">
					<xsl:call-template name="wordcounts">
						<xsl:with-param name="counts" select="." />
						<xsl:with-param name="name" ><xsl:value-of select="$document/@nameLong"/></xsl:with-param>
						<xsl:with-param name="id" ><xsl:value-of select="$document/@id"/></xsl:with-param>
					</xsl:call-template>
				</xsl:if>
			</xsl:for-each>
	   </TBODY>
	</TABLE>
	</P>
</xsl:template>
	
<!-- create summary --> 
<xsl:template name = "summary" >
	<P><BR/></P>
	<TABLE border="1">
	   <TBODY>
		  <TR>
			 <TD bgcolor="#d3d3d3">Word Count: </TD>
			 <TD bgcolor="#f7f7f7">Summary</TD>
		  </TR>
		  <TR>
			 <TD bgcolor="#d3d3d3">Folder:</TD>
			 <TD bgcolor="#f7f7f7"><xsl:value-of select="/tmwordcount/description/folder/@nameLong"/></TD>
		  </TR>
		  <TR>
			 <TD bgcolor="#d3d3d3">Counted:</TD>
			 <TD bgcolor="#f7f7f7">Source Words</TD>
		  </TR>
	   </TBODY>
	</TABLE>
	<TABLE border="1" cellspacing="3">
	   <TBODY>
     	    <xsl:call-template name="countsheader">
				<xsl:with-param name="factsheet">No</xsl:with-param>
			</xsl:call-template>
			<xsl:for-each select="/tmwordcount/summary/document">
					<xsl:call-template name="wordcounts">
						<xsl:with-param name="counts" select="./wordcount" />
						<xsl:with-param name="name" ><xsl:value-of select="@name"/></xsl:with-param>
						<xsl:with-param name="id" ><xsl:value-of select="@id"/></xsl:with-param>
					</xsl:call-template>
			</xsl:for-each>
	   </TBODY>
	</TABLE>
</xsl:template>

<!-- setup table column header base on the column listt --> 
<xsl:template name = "countsheader" >
	<xsl:param name="factsheet"></xsl:param>
		  <TR>
			 <xsl:if test="$factsheet != 'Yes'" >
				 <TD rowspan="2" valign="top" bgcolor="#d3d3d3" align="center">Doc<BR/>Id</TD>
				 <TD rowspan="2" valign="top" bgcolor="#d3d3d3">Document</TD>
				 <TD rowspan="2" valign="top" bgcolor="#d3d3d3" align="center">Shipment<BR/>Number</TD>
			 </xsl:if>
			 <xsl:if test="$factsheet = 'Yes'" >
				 <TD rowspan="2" valign="top" bgcolor="#d3d3d3" align="center">Factors</TD>
			 </xsl:if>
			 <TD rowspan="2" valign="top" bgcolor="#d3d3d3">Category</TD>
			 <TD rowspan="2" valign="top" bgcolor="#d3d3d3" align="center">Fuzzy<BR/>Level</TD>

			 <xsl:if test="count(/tmwordcount/columnList/column[(@group ='Analysis') and (@shrink !='Yes')]) != 0" >
			   <TD align="center" bgcolor="#d3d3d3">
				 <xsl:attribute name="colspan"><xsl:value-of select="count(/tmwordcount/columnList/column[(@group ='Analysis') and (@shrink !='Yes')])"/></xsl:attribute>
				 Analysis
		   	   </TD>
			 </xsl:if>
     	     <xsl:if test="count(/tmwordcount/columnList/column[(@group ='Edit') and (@shrink !='Yes')]) != 0" >
 			   <TD align="center" bgcolor="#d3d3d3">
				   <xsl:attribute name="colspan"><xsl:value-of select="count(/tmwordcount/columnList/column[(@group ='Edit') and (@shrink !='Yes')])"/></xsl:attribute>
				   Edit</TD>
 			</xsl:if>
			<xsl:if test="/tmwordcount/columnList/column[@name='NotTranslated']/@shrink !='Yes'" >
				 <TD rowspan="2" valign="top" bgcolor="#d3d3d3" align="center">Not<BR/>Translated</TD>
			</xsl:if>
		  </TR>
		  <TR>
			<xsl:if test="/tmwordcount/columnList/column[@name='SumAnalyze']/@shrink !='Yes'" >
				 <TD align="center" bgcolor="#d3d3d3">Sum<BR/>Analyze</TD>
			</xsl:if>
			<xsl:if test="/tmwordcount/columnList/column[@name='AnalyzeAuto']/@shrink !='Yes'" >
				 <TD align="center" bgcolor="#d3d3d3">Auto<BR/>Subst</TD>
			</xsl:if>
			<xsl:if test="/tmwordcount/columnList/column[@name='AnalyzePost']/@shrink !='Yes'" >
				 <TD align="center" bgcolor="#d3d3d3">Auto<BR/>Subst2</TD>
			</xsl:if>
			<xsl:if test="/tmwordcount/columnList/column[@name='SumEdit']/@shrink !='Yes'" >
				 <TD align="center" bgcolor="#d3d3d3">Sum<BR/>Edit</TD>
			</xsl:if>
			<xsl:if test="/tmwordcount/columnList/column[@name='AnalyzeEdit']/@shrink !='Yes'" >
			    <TD align="center" bgcolor="#d3d3d3">Auto<BR/>Subst</TD>
			</xsl:if>
			<xsl:if test="/tmwordcount/columnList/column[@name='EditExact']/@shrink !='Yes'" >
				 <TD align="center" bgcolor="#d3d3d3">Exact<BR/>Matches</TD>
			</xsl:if>
			<xsl:if test="/tmwordcount/columnList/column[@name='EditReplace']/@shrink !='Yes'" >
				 <TD align="center" bgcolor="#d3d3d3">Replace<BR/>Matches</TD>
			</xsl:if>
			<xsl:if test="/tmwordcount/columnList/column[@name='SumFuzzy']/@shrink !='Yes'" >
				 <TD align="center" bgcolor="#d3d3d3">Sum<BR/>Fuzzy</TD>
			</xsl:if>
			<xsl:if test="/tmwordcount/columnList/column[@name='Fuzzy5069']/@shrink !='Yes'" >
				 <TD align="center" bgcolor="#d3d3d3">Fuzzy<BR/>Matches<BR/>50-70%</TD>
			</xsl:if>
			<xsl:if test="/tmwordcount/columnList/column[@name='Fuzzy7089']/@shrink !='Yes'" >
				 <TD align="center" bgcolor="#d3d3d3">Fuzzy<BR/>Matches<BR/>71-90%</TD>
			</xsl:if>
			<xsl:if test="/tmwordcount/columnList/column[@name='Fuzzy9099']/@shrink !='Yes'" >
				 <TD align="center" bgcolor="#d3d3d3">Fuzzy<BR/>Matches<BR/>&gt;90%</TD>
			</xsl:if>
			<xsl:if test="/tmwordcount/columnList/column[@name='ChangedFuzzy']/@shrink !='Yes'" >
				 <TD align="center" bgcolor="#d3d3d3">Changed<BR/>Fuzzy</TD>
			</xsl:if>
			<xsl:if test="/tmwordcount/columnList/column[@name='MachineMatches']/@shrink !='Yes'" >
				 <TD align="center" bgcolor="#d3d3d3">Machine<BR/>Matches</TD>
			</xsl:if>
			<xsl:if test="/tmwordcount/columnList/column[@name='NoneMatches']/@shrink !='Yes'" >
			 <TD align="center" bgcolor="#d3d3d3">Manually<BR/>Translated</TD>
			</xsl:if>
		  </TR>
</xsl:template>
	
<!-- create fact sheet --> 
<xsl:template name = "factsheet" >
	<P><BR/></P>
	<TABLE border="1">
	   <TBODY>
		  <TR>
			 <TD bgcolor="#d3d3d3">Word Count: </TD>
			 <TD bgcolor="#f7f7f7">Fact Sheet</TD>
		  </TR>
		  <TR>
			 <TD bgcolor="#d3d3d3">Folder:</TD>
			 <TD bgcolor="#f7f7f7"><xsl:value-of select="/tmwordcount/description/folder/@nameLong"/></TD>
		  </TR>
		  <TR>
			 <TD bgcolor="#d3d3d3">Counted:</TD>
			 <TD bgcolor="#f7f7f7">Source Words</TD>
		  </TR>
	   </TBODY>
	</TABLE>

	<TABLE border="1" cellspacing="3">
	   <TBODY>
     	    <xsl:call-template name="countsheader">
				<xsl:with-param name="factsheet">Yes</xsl:with-param>
			</xsl:call-template>
			<xsl:for-each select="/tmwordcount/factsheet/group">
					<xsl:call-template name="wordcounts">
						<xsl:with-param name="counts" select="./wordcount" />
						<xsl:with-param name="name" ><xsl:value-of select="@name"/></xsl:with-param>
						<xsl:with-param name="id" ></xsl:with-param>
					</xsl:call-template>
			</xsl:for-each>
	   </TBODY>
	</TABLE>
</xsl:template>

<!-- display all  categories -->      
<xsl:template name = "wordcounts" >
	<xsl:param name="counts"></xsl:param>
	<xsl:param name="name"></xsl:param>
	<xsl:param name="id"></xsl:param>
	
	<xsl:if test="/tmwordcount/description/report/@catDetails='true'" >
		<tr align="right">
			<xsl:call-template name="wordcount">
				<xsl:with-param name="counts" select="$counts" />
				<xsl:with-param name="name" ><xsl:value-of select="$name"/></xsl:with-param>
				<xsl:with-param name="cat" >Simple</xsl:with-param>
				<xsl:with-param name="id" ><xsl:value-of select="$id"/></xsl:with-param>
			</xsl:call-template>
		</tr>
		
		<tr align="right">
			<xsl:call-template name="wordcount">
				<xsl:with-param name="counts" select="$counts" />
				<xsl:with-param name="name" ></xsl:with-param>
				<xsl:with-param name="cat" >Medium</xsl:with-param>
				<xsl:with-param name="id" ></xsl:with-param>
			</xsl:call-template>
		</tr>
		
		<tr align="right">
			<xsl:call-template name="wordcount">
				<xsl:with-param name="counts" select="$counts" />
				<xsl:with-param name="name" ></xsl:with-param>
				<xsl:with-param name="cat" >Complex</xsl:with-param>
				<xsl:with-param name="id" ></xsl:with-param>
			</xsl:call-template>
		</tr>
	</xsl:if>
	
	<xsl:if test="/tmwordcount/description/report/@catSum='true'" >
		<tr align="right" bgcolor="#d3d3d3">
			<xsl:choose>
				<xsl:when test="/tmwordcount/description/report/@catDetails='true'"> 
					<xsl:call-template name="wordcount">
						<xsl:with-param name="counts" select="$counts" />
						<xsl:with-param name="name"></xsl:with-param>
						<xsl:with-param name="cat" >Sum</xsl:with-param>
						<xsl:with-param name="id" ></xsl:with-param>
					</xsl:call-template>
				</xsl:when>
				<xsl:otherwise> 
				<xsl:call-template name="wordcount">
					<xsl:with-param name="counts" select="$counts" />
					<xsl:with-param name="name" select="$name"/>
					<xsl:with-param name="cat" >Sum</xsl:with-param>
					<xsl:with-param name="id"  select="$id"/>
				</xsl:call-template>
				</xsl:otherwise>
			</xsl:choose> 
		</tr>
	</xsl:if>
	
</xsl:template>
      
<!-- display the counts of a single category -->      
<xsl:template name = "wordcount" >
	<xsl:param name="counts"></xsl:param>
	<xsl:param name="name"></xsl:param>
	<xsl:param name="cat"></xsl:param>
	<xsl:param name="id"></xsl:param>
	
	<!-- show row spanning cells  -->      
	<xsl:choose>
		<xsl:when test="(/tmwordcount/description/report/@catSum='true') and (/tmwordcount/description/report/@catDetails='true')">
			<xsl:call-template name="rowspancells">
				<xsl:with-param name="counts" select="$counts" />
				<xsl:with-param name="name" select="$name" />
				<xsl:with-param name="id" select="$id" />
				<xsl:with-param name="rowspan">4</xsl:with-param>
			</xsl:call-template>
		</xsl:when>
		<xsl:when test="/tmwordcount/description/report/@catSum='true'">
			<xsl:call-template name="rowspancells">
				<xsl:with-param name="counts" select="$counts" />
				<xsl:with-param name="name" select="$name" />
				<xsl:with-param name="id" select="$id" />
				<xsl:with-param name="rowspan">1</xsl:with-param>
			</xsl:call-template>
		</xsl:when>
		<xsl:when test="/tmwordcount/description/report/@catDetails='true'">
			<xsl:call-template name="rowspancells">
				<xsl:with-param name="counts" select="$counts" />
				<xsl:with-param name="name" select="$name" />
				<xsl:with-param name="id" select="$id" />
				<xsl:with-param name="rowspan">3</xsl:with-param>
			</xsl:call-template>
		</xsl:when>
		<xsl:otherwise>
			<xsl:call-template name="rowspancells">
				<xsl:with-param name="counts" select="$counts" />
				<xsl:with-param name="name" select="$name" />
				<xsl:with-param name="id" select="$id" />
				<xsl:with-param name="rowspan">1</xsl:with-param>
			</xsl:call-template>
		</xsl:otherwise>
	</xsl:choose>
  
	
	<!-- category text -->
	<td>		
		<xsl:choose>
			<xsl:when test="$cat = 'Simple'">1..4</xsl:when>
			<xsl:when test="$cat = 'Medium'">5..14</xsl:when>
			<xsl:when test="$cat = 'Complex'">&gt;14</xsl:when>
			<xsl:when test="$cat = 'Sum'">
				<xsl:choose>
					<xsl:when test="$name = 'ComplexityFactor'">Mean comp</xsl:when>
					<xsl:when test="$name = 'PayFactor'">Mean pay</xsl:when>
					<xsl:otherwise>Sum</xsl:otherwise>
				</xsl:choose>
			</xsl:when>
			<xsl:otherwise></xsl:otherwise>
		</xsl:choose>
		&nbsp;
	</td>

	<!-- fuzzy level -->
	<td><xsl:value-of select="$counts/fuzzy[@category = $cat]/@levelPerCent"/>&nbsp;</td>

	<!-- the count values -->
	<xsl:if test="/tmwordcount/columnList/column[@name='SumAnalyze']/@shrink !='Yes'" >
		<td><xsl:value-of select="$counts/words[@category=$cat and @type='SumAnalyze']/@count" />&nbsp;</td>
	</xsl:if>
	<xsl:if test="/tmwordcount/columnList/column[@name='AnalyzeAuto']/@shrink !='Yes'" >
		<td><xsl:value-of select="$counts/words[@category=$cat and @type='AnalyzeAuto']/@count" />&nbsp;</td>
	</xsl:if>
	<xsl:if test="/tmwordcount/columnList/column[@name='AnalyzePost']/@shrink !='Yes'" >
		<td><xsl:value-of select="$counts/words[@category=$cat and @type='AnalyzePost']/@count" />&nbsp;</td>
	</xsl:if>
	<xsl:if test="/tmwordcount/columnList/column[@name='SumEdit']/@shrink !='Yes'" >
		<td><xsl:value-of select="$counts/words[@category=$cat and @type='SumEdit']/@count" />&nbsp;</td>
	</xsl:if>
	<xsl:if test="/tmwordcount/columnList/column[@name='AnalyzeEdit']/@shrink !='Yes'" >
		<td><xsl:value-of select="$counts/words[@category=$cat and @type='AnalyzeEdit']/@count" />&nbsp;</td>
	</xsl:if>
	<xsl:if test="/tmwordcount/columnList/column[@name='EditExact']/@shrink !='Yes'" >
		<td><xsl:value-of select="$counts/words[@category=$cat and @type='EditExact']/@count" />&nbsp;</td>
	</xsl:if>
	<xsl:if test="/tmwordcount/columnList/column[@name='EditReplace']/@shrink !='Yes'" >
		<td><xsl:value-of select="$counts/words[@category=$cat and @type='EditReplace']/@count" />&nbsp;</td>
	</xsl:if>
	<xsl:if test="/tmwordcount/columnList/column[@name='SumFuzzy']/@shrink !='Yes'" >
		<td><xsl:value-of select="$counts/words[@category=$cat and @type='SumFuzzy']/@count" />&nbsp;</td>
	</xsl:if>
	<xsl:if test="/tmwordcount/columnList/column[@name='Fuzzy5069']/@shrink !='Yes'" >
		<td><xsl:value-of select="$counts/words[@category=$cat and @type='Fuzzy5069']/@count" />&nbsp;</td>
	</xsl:if>
	<xsl:if test="/tmwordcount/columnList/column[@name='Fuzzy7089']/@shrink !='Yes'" >
		<td><xsl:value-of select="$counts/words[@category=$cat and @type='Fuzzy7089']/@count" />&nbsp;</td>
	</xsl:if>
	<xsl:if test="/tmwordcount/columnList/column[@name='Fuzzy9099']/@shrink !='Yes'" >
		<td><xsl:value-of select="$counts/words[@category=$cat and @type='Fuzzy9099']/@count" />&nbsp;</td>
	</xsl:if>
	<xsl:if test="/tmwordcount/columnList/column[@name='ChangedFuzzy']/@shrink !='Yes'" >
		<td><xsl:value-of select="$counts/words[@category=$cat and @type='ChangedFuzzy']/@count" />&nbsp;</td>
	</xsl:if>
	<xsl:if test="/tmwordcount/columnList/column[@name='MachineMatches']/@shrink !='Yes'" >
		<td><xsl:value-of select="$counts/words[@category=$cat and @type='MachineMatches']/@count" />&nbsp;</td>
	</xsl:if>
	<xsl:if test="/tmwordcount/columnList/column[@name='NoneMatches']/@shrink !='Yes'" >
		<td><xsl:value-of select="$counts/words[@category=$cat and @type='NoneMatches']/@count" />&nbsp;</td>
	</xsl:if>
	<xsl:if test="/tmwordcount/columnList/column[@name='NotTranslated']/@shrink !='Yes'" >
		<td><xsl:value-of select="$counts/words[@category=$cat and @type='NotTranslated']/@count" />&nbsp;</td>
	</xsl:if>
</xsl:template>

<!-- show rowspan cells of a count line  -->      

<xsl:template name = "rowspancells" >
	<xsl:param name="counts"></xsl:param>
	<xsl:param name="name"></xsl:param>
	<xsl:param name="id"></xsl:param>
	<xsl:param name="rowspan"></xsl:param>
	
<xsl:if test="$id !=''" >
		 <td  bgcolor="#f7f7f7"><xsl:attribute name="rowspan"><xsl:value-of select="$rowspan"/></xsl:attribute>
			 <xsl:if test="$id !='0'" >
				 <xsl:value-of select="$id"/>
			</xsl:if>
			 <xsl:if test="$id ='0'" >
				 <xsl:text>&nbsp;</xsl:text>
			</xsl:if>
		</td>
	</xsl:if>
	<xsl:if test="$name !=''" >
		<td  bgcolor="#f7f7f7"><xsl:attribute name="rowspan"><xsl:value-of select="$rowspan"/></xsl:attribute>			
			<xsl:choose>
			<xsl:when test="$name = 'ActualWords'">Actual Words</xsl:when>
			<xsl:when test="$name = 'ComplexityFactor'">Complexity<BR/>Factor</xsl:when>
			<xsl:when test="$name = 'WeightedWords'">Complexity<BR/>Factor x <BR/>Actual Words.</xsl:when>
			<xsl:when test="$name = 'PayFactor'">Payment<BR/>factor</xsl:when>
			<xsl:when test="$name = 'PayWords'">Payable Words</xsl:when>
			<xsl:otherwise><xsl:value-of select="$name"/></xsl:otherwise>
			</xsl:choose>
		 </td>
	</xsl:if>
	<xsl:if test="$id !=''" >
		 <td><xsl:attribute name="rowspan"><xsl:value-of select="$rowspan"/></xsl:attribute><xsl:value-of select="$counts/@shipmentDocument"/>&nbsp;</td>
	</xsl:if>   
</xsl:template>
      
<xsl:template name = "finalsummary" >
	<P><BR/></P>
	<TABLE border="1">
	   <TBODY>
		  <TR>
			 <TD bgcolor="#d3d3d3">Report:</TD>
			 <TD bgcolor="#f7f7f7">Final Summary</TD>
		  </TR>
		  <TR>
			 <TD bgcolor="#d3d3d3">Shipments:</TD>
			 <TD bgcolor="#f7f7f7"><xsl:value-of select="/tmwordcount/finalSummary/@shipment"/>&nbsp;</TD>
		  </TR>
		  <TR>
			 <TD bgcolor="#d3d3d3">Payable words:</TD>
			 <TD bgcolor="#f7f7f7" align="right"><xsl:value-of select="/tmwordcount/finalSummary/@payableWords"/></TD>
		  </TR>
		  <TR>
			 <TD bgcolor="#d3d3d3">Local Currency:</TD>
			 <TD bgcolor="#f7f7f7" align="right"><B><xsl:value-of select="/tmwordcount/finalSummary/@localCurrency"/></B></TD>
		  </TR>
		  <TR>
			 <TD bgcolor="#d3d3d3">Total Payment:</TD>
			 <TD bgcolor="#cccc00" align="right"><B><xsl:value-of select="/tmwordcount/finalSummary/@totalPay"/></B></TD>
		  </TR>
	   </TBODY>
	</TABLE></xsl:template>
	
<xsl:template name = "documentlist" >
	<P><BR/></P>
	<TABLE border="1">
	   <TBODY>
		  <TR>
			 <TD bgcolor="#d3d3d3">Report:</TD>
			 <TD bgcolor="#f7f7f7">Document List</TD>
		  </TR>
	   </TBODY>
	</TABLE>
	<TABLE border="1">
	   <TBODY>
     	  <xsl:for-each select="/tmwordcount/documentList/document">
			  <TR><td>
				<TABLE border="0" cellspacing="3">
				   <TBODY>
					<TR>
					   <TD>&nbsp;</TD>
					   <TD><xsl:value-of select="./@num"/>&nbsp;</TD>
					</TR>
					<xsl:if test="./path!=''">
						<TR>
						   <TD>Path: </TD>
						   <TD><xsl:value-of select="./path"/>&nbsp;</TD>
						</TR>
					</xsl:if>
					<TR>
					   <TD>Name: </TD>
					   <TD><xsl:value-of select="./name"/>&nbsp;</TD>
					</TR>
				   </TBODY>
				</TABLE>
			  </td></TR>
	      </xsl:for-each>
	   </TBODY>
	</TABLE>
</xsl:template>
	
</xsl:stylesheet>
